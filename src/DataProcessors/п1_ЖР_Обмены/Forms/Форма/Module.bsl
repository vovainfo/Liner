#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура ЗарегистрироватьВОбмене(Команда)
	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат;		
	КонецЕсли;

	ЧислоЭлементов = ЗарегистрироватьИзмененияСправочниковЗаПериод();
	
	Сообщение = Новый СообщениеПользователю();
	Сообщение.Текст = СтрШаблон(НСтр("ru='Подготовлено к обмену %1 элементов справочников'"), ЧислоЭлементов);
	Сообщение.Сообщить();
КонецПроцедуры
#КонецОбласти


#Область СлужебныеПроцедурыИФункции
&НаСервере
Функция ЗарегистрироватьИзмененияСправочниковЗаПериод()
	СоставПланаОбмена = Новый Массив;
	Для каждого Элемент из Метаданные.ПланыОбмена.ПланОбменаСправочники.Состав Цикл
		СоставПланаОбмена.Добавить(Элемент.Метаданные.ПолноеИмя());		
	КонецЦикла;
	МассивСобытия = СтрРазделить("_$Data$_.Update,_$Data$_.New", ",");
	
	Отбор = Новый Структура();
	Отбор.Вставить("Метаданные", СоставПланаОбмена);
	Отбор.Вставить("Событие", МассивСобытия);
	Отбор.Вставить("СтатусТранзакции", СтатусТранзакцииЗаписиЖурналаРегистрации.Зафиксирована);
	Отбор.Вставить("ДатаНачала", НачалоПериода);
	Отбор.Вставить("ДатаОкончания", ОкончаниеПериода);
	
	ЖурналРегистрации = Новый ТаблицаЗначений();
	ВыгрузитьЖурналРегистрации(ЖурналРегистрации, Отбор, "Данные");

	ЖурналРегистрации.Свернуть("Данные");

	УзелОбмена = ПланыОбмена.ПланОбменаСправочники.НайтиПоНаименованию("ДалекаяБаза");
	Для Каждого стр Из ЖурналРегистрации Цикл
		ПланыОбмена.ЗарегистрироватьИзменения(УзелОбмена, стр.Данные);
	КонецЦикла;
	
	Возврат ЖурналРегистрации.Количество(); 
КонецФункции
#КонецОбласти
