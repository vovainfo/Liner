
#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура СторнироватьВыделенные(Команда)
	Если НЕ ЗначениеЗаполнено(ТЗ) Тогда
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = НСтр("ru='Список документов не загружен'");
		Сообщение.Сообщить();
	КонецЕсли;
	
	СторнироватьВыделенныеНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура Отобрать(Команда)
	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат;		
	КонецЕсли;

	ЗагрузитьСписокДокументов();	
КонецПроцедуры

&НаКлиенте
Процедура ОтметитьВсе(Команда)
	УстановитьОтметку(Истина);
КонецПроцедуры

&НаКлиенте
Процедура СнятьВсеОтметки(Команда)
	УстановитьОтметку(Ложь);
КонецПроцедуры
#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗагрузитьСписокДокументов()
	ТЗ.Очистить();
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Сторно.ДокументОснование
		|ПОМЕСТИТЬ втСторнированные
		|ИЗ
		|	Документ.Сторно КАК Сторно
		|ГДЕ
		|	Сторно.Проведен
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПриходнаяНакладная.Дата КАК Дата,
		|	ПриходнаяНакладная.Ссылка КАК Документ,
		|	ПриходнаяНакладная.Контрагент,
		|	ПриходнаяНакладная.Склад,
		|	ЛОЖЬ КАК Отметка
		|ИЗ
		|	Документ.ПриходнаяНакладная КАК ПриходнаяНакладная
		|ГДЕ
		|	ПриходнаяНакладная.Проведен
		|	И (ПриходнаяНакладная.Контрагент = &Контрагент
		|	ИЛИ &НетОтбораПоКонтрагенту)
		|	И (ПриходнаяНакладная.Склад = &Склад
		|	ИЛИ &НетОтбораПоСкладу)
		|	И ПриходнаяНакладная.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И ПриходнаяНакладная.Ссылка НЕ В
		|		(ВЫБРАТЬ
		|			ДокументОснование
		|		ИЗ
		|			втСторнированные)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Дата";
	
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("НетОтбораПоКонтрагенту", НЕ ЗначениеЗаполнено(Контрагент));
	Запрос.УстановитьПараметр("НетОтбораПоСкладу", НЕ ЗначениеЗаполнено(Склад));
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ТЗ.Добавить(), Выборка);
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура УстановитьОтметку(Значение)
	Для Каждого стр Из ТЗ Цикл
		стр.Отметка = Значение;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура СторнироватьВыделенныеНаСервере()
	ВыбранныеСтроки = ТЗ.НайтиСтроки(Новый Структура("Отметка", Истина));
	Для Каждого стр Из ВыбранныеСтроки Цикл
		СторнироватьДокумент(стр.Документ);
	КонецЦикла;	
КонецПроцедуры

&НаСервере
Процедура СторнироватьДокумент(Док)
	НовыйДокумент = Документы.Сторно.СоздатьДокумент();
	НовыйДокумент.Дата = ТекущаяДатаСеанса();
	НовыйДокумент.Заполнить(Док);
	НовыйДокумент.Записать(РежимЗаписиДокумента.Проведение);
	Сообщение = Новый СообщениеПользователю();
	Сообщение.Текст = Строка(НовыйДокумент);
	Сообщение.Сообщить();	
КонецПроцедуры

#КонецОбласти
