#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	БезопасныйРежим = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если ЗначениеЗаполнено(Путь) Тогда
		ЗаполнитьСписокФайлов();
	КонецЕсли;
КонецПроцедуры
#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы
&НаКлиенте
Асинх Процедура ПутьНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	Диалог.Каталог = Путь;
	Диалог.Заголовок = НСтр("ru='Укажите каталог с отчетами'"); 
	Результат = Ждать Диалог.ВыбратьАсинх();
	Если ЗначениеЗаполнено(Результат) Тогда
		Путь = Результат[0];
		ЗаполнитьСписокФайлов();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПутьПриИзменении(Элемент)
	ЗаполнитьСписокФайлов();
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокОтчетов
&НаКлиенте
Процедура СписокОтчетовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Перем ПолныйПуть;
	
	Если Прав(Элемент.ТекущиеДанные.Значение, 1) = "\" Тогда
		ПолныйПуть = Путь + Элемент.ТекущиеДанные.Значение;
	Иначе
		ПолныйПуть = Путь + "\" + Элемент.ТекущиеДанные.Значение;
	КонецЕсли;

	ОткрытьВнешнююОбработку(ПолныйПуть);
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаКлиенте
Асинх Процедура ЗаполнитьСписокФайлов()
	СписокОтчетов.Очистить();
	МассивФайлов = Ждать НайтиФайлыАсинх(Путь, "*.erf", Ложь);
	Для Каждого Файл Из МассивФайлов Цикл
		СписокОтчетов.Добавить(Файл.Имя);
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьВнешнююОбработку(ПолныйПуть)
	
    ОбработкаОкончанияПомещения = Новый ОписаниеОповещения("ОбработчикОкончанияПомещения", ЭтотОбъект);
    НачатьПомещениеФайлаНаСервер(ОбработкаОкончанияПомещения,,,, ПолныйПуть, УникальныйИдентификатор);
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикОкончанияПомещения(ОписаниеПомещенногоФайла, ДополнительныеПараметры) Экспорт
	Если ОписаниеПомещенногоФайла = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
    ИмяОтчета = ПодключитьВнешнийОтчет(ОписаниеПомещенногоФайла.Адрес, БезопасныйРежим);
    ОткрытьФорму("ВнешнийОтчет."+ ИмяОтчета +".Форма");
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПодключитьВнешнийОтчет(АдресХранилища, БезопасныйРежим)
    Возврат ВнешниеОтчеты.Подключить(АдресХранилища, , БезопасныйРежим);
КонецФункции 
#КонецОбласти
